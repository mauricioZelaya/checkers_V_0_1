buildscript {
    ext {
        depManagementVersion = "1.0.3.RELEASE"
    }

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
    }

    dependencies {
        classpath ("io.spring.gradle:dependency-management-plugin:${depManagementVersion}")
        classpath 'net.saliman:gradle-cobertura-plugin:2.4.0'
    }
}

allprojects {
    group 'org.checkers'
    version '1.0-SNAPSHOT'
}

subprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
    }

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'findbugs'
    apply plugin: "net.saliman.cobertura"
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    ext {
        SLF4J_VERSION = "1.7.10"
        JUNIT_VERSION = "4.12"
        CHECKSTYLE_VERSION = "8.0"
    }

    dependencyManagement {
        imports {
            mavenBom 'io.spring.platform:platform-bom:1.1.2.RELEASE'
        }
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled false
            html.enabled true
        }
    }

    tasks.withType(Checkstyle) {
        reports {
            xml.enabled false
            html.enabled true
        }
    }

    tasks.withType(Pmd) {
        reports {
            xml.enabled false
            html.enabled true
        }
    }

    task fatJar(type: Jar) {
        manifest {
            //attributes 'Main-Class': 'com.Main'
        }
        baseName = project.name + '-all'
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
        with jar
    }

    dependencies {
        compile "org.slf4j:slf4j-api:${SLF4J_VERSION}"
        compile "org.slf4j:slf4j-log4j12:${SLF4J_VERSION}"

        testCompile group: 'junit', name: 'junit', version: '4.+'
    }

    checkstyle {
        toolVersion = CHECKSTYLE_VERSION
        config = rootProject.resources.text.fromFile("config/checkstyle/checkstyle.xml")
    }
}